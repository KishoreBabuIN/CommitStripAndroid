apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {
        defaultPublishConfig 'debug'

        applicationId "com.kishorebabu.android.commitstrip"
        minSdkVersion 19
        targetSdkVersion 26
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"

        def MAJOR_VERSION = 1
        def MINOR_VERSION = 0
        def BUILD_NUMBER = System.getenv("TRAVIS_BUILD_NUMBER") as Integer ?: 1
        def versionInt = (MAJOR_VERSION * 100000) + (MINOR_VERSION * 1000) + BUILD_NUMBER

        versionCode versionInt
        versionName "$MAJOR_VERSION.$MINOR_VERSION.$BUILD_NUMBER"

        buildConfigField("String", "TWITTER_CONSUMER_KEY", "\"${TwitterConsumerKey}\"")
        buildConfigField("String", "TWITTER_CONSUMER_SECRET", "\"${TwitterConsumerSecret}\"")

        resConfigs "en"

        ndk {
            abiFilters "armeabi-v7a", "x86", "arm64-v8a"
        }

        def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
        def buildTime = new GregorianCalendar().format("dd-MM-yyyy ' ' h:mm:ss a z")
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    buildTypes {
        release {
            resValue "string", "app_name", "CommitStrip"
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            resValue "string", "app_name", "CommitStrip Debug"

            versionNameSuffix " Debug"
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:1.11.0'
        force "com.squareup.okhttp3:okhttp:3.5.0"
        force 'org.jetbrains.kotlin:kotlin-stdlib:1.1.2-3'
    }
}

apply from: 'dependencies.gradle'

dependencies {

    implementation supportLibs
    implementation networkLibs
    implementation rxJavaLibs

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') { transitive = true }

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    implementation "com.jakewharton.timber:timber:4.5.1"
    implementation('com.twitter.sdk.android:twitter-core:3.1.1@aar') { transitive = true }

    //Room
    implementation 'android.arch.persistence.room:runtime:1.0.0-alpha5'
    implementation "android.arch.persistence.room:rxjava2:1.0.0-alpha5"
    kapt 'android.arch.persistence.room:compiler:1.0.0-alpha5'

    implementation "io.reactivex.rxjava2:rxjava:2.1.3"

    implementation 'com.github.chrisbanes:PhotoView:2.1.3'


    implementation "com.google.dagger:dagger:$versions.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    debugImplementation "com.facebook.stetho:stetho-okhttp3:1.4.2"
    debugImplementation leakCannary
    releaseImplementation leakCannary
    testImplementation leakCannary

    // APT dependencies
    kapt daggerCompiler
    kaptTest daggerCompiler
    kaptAndroidTest daggerCompiler

    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    testImplementation "org.jetbrains.kotlin:kotlin-test:1.1.2-2"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.1.2-2"
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}